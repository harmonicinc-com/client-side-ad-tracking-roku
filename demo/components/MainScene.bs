sub init()
    m.video = m.top.findNode("video")
    m.video.setFocus(true)
    m.video.enableLiveAvailabilityWindow = true
    m.video.content = CreateObject("roSGNode", "ContentNode")
    m.video.content.live = true
    m.video.content.playStart = 2147483647 ' Workaround Roku player doesn't jump to live edge

    ' Init ad beaconing overlay
    m.adBeaconOverlay = CreateObject("roSGNode", "AdBeaconOverlay")
    m.adBeaconOverlay.video = m.video
    m.video.AppendChild(m.adBeaconOverlay)

    ' Init error overlay
    m.errorOverlay = CreateObject("roSGNode", "ErrorOverlay")
    m.video.AppendChild(m.errorOverlay)

    ' Start player task
    m.playerTask = createObject("roSGNode", "PlayerTask")
    m.playerTask.video = m.video
    m.playerTask.control = "run"
    m.playerTask.observeField("adExited", "onAdExited")
    m.playerTask.observeField("adPods", "onAdPodsUpdated")
    m.playerTask.observeField("errors", "onErrorsUpdated")
end sub

sub onAdPodsUpdated()
    if m.adBeaconOverlay <> invalid
        m.adBeaconOverlay.pods = m.playerTask.adPods
    end if
end sub

sub onErrorsUpdated()
    if m.errorOverlay <> invalid
        m.errorOverlay.errors = m.playerTask.errors
    end if
end sub

' Invoked when back button was pressed during ads
sub onAdExited()
    ' Do your stuff here
    m.video.setFocus(true)
end sub
